---
layout: post
title: SED
date: 2023-03-24 12:37 +0000
categories:
  - 系统
  - Unix/Linux
tags:
  - linux
  - shell
  - sed
---

SED 的日常操作。

## 删除匹配行

`d` 删除匹配的行。

```bash
cat <<EOF | sed '/^\s*key1:/d'
section1:
  key1: value1
  key2: value2
section2:
  key1: value1
  key2: value2
EOF
```

## 删除不匹配行

`!`表示不匹配，`!d`表示删除匹配的行。

```bash
cat <<EOF | sed '/^\s*key1:/!d'
section1:
  key1: value1
  key2: value2
section2:
  key1: value1
  key2: value2
EOF
```

## 替换匹配行

`c\` 表示替换匹配行。

```bash
cat <<EOF | sed '/#key3:/c\  key3: value3'
section1:
  key1: value1
  key2: value2
  #key3: value3
section2:
  key4=value4
EOF
```

通过`\1`引用第 1 对`\(`和`\)`之间的匹配值，依次类推。

```bash
cat <<EOF | sed 's/^\(\s*\)#\(.*\)$/\1\2/g'
section1:
  key1: value1
  key2: value2
  #key3: value3
section2:
  key4=value4
EOF
```

## 插入到匹配行之前

`i\` 表示插入到匹配行之前。

```bash
cat <<EOF | sed '/section1:/i\common:'
section1:
  key1: value1
  key2: value2
  #key3: value3
section2:
  key4=value4
EOF
```

## 追加到匹配行之后

`a\` 表示追加到匹配行之后。

```bash
cat <<EOF | sed '/common:/a\  key: value'
common:
section1:
  key1: value1
  key2: value2
  #key3: value3
section2:
  key4=value4
EOF
```

## 复杂的模式匹配操作

先看看通用的例子：

```bash
sed '/pattern1/,/pattern2/ {
  ...
  /pattern3/,/pattern4/ {
    ...
  }
}' file
```

通过一些简单操作的无限组合，可完成非常复杂的操作。一些特殊的匹配规则如下：

- `/pattern/`：模式匹配`pattern`内容，其中`1`和`$`表示匹配首行和尾行；
- `/pattern/!`：模式不匹配`pattern`内容；
- `/pattern1/,/pattern2/{...}`：模式匹配`pattern1`和`pattern2`之间内容，`{...}`内的操作只对匹配内容有效，可无限嵌套。

删除首行到第 N 行内容：

```bash
cat <<EOF | sed '1,+2d'
section1:
  key1: value1
  key2: value2
section2:
  key1: value1
  key2: value2
EOF
```

删除匹配行到尾行内容：

```bash
cat <<EOF | sed '/section2:/,$d'
section1:
  key1: value1
  key2: value2
  #key3: value3
section2:
  key4=value4
EOF
```

也可以这样：

```bash
cat <<EOF | sed '/section2:/,$ {
  /.*/d
}'
section1:
  key1: value1
  key2: value2
  #key3: value3
section2:
  key4=value4
EOF
```

`/.*/d`可以简写为`d`。

删除两个模式匹配行之间的内容：

```bash
cat <<EOF | sed '/section1:/,/section2:/{
  /section1:/!{
    /section2:/!d
  }
}'
section1:
  key1: value1
  key2: value2
  #key3: value3
section2:
  key4=value4
EOF
```

替换两个模式匹配行之间的内容：

```bash
cat <<EOF | sed '/section1:/,/section2:/ {
  s#key1#key3#g
}'
section1:
  key1: value1
  key2: value2
section2:
  key1: value1
  key2: value2
EOF
```

也可以这样：

```bash
cat <<EOF | sed '/section1:/ {
  :a
  n
  s#key1#key3#g
  /section2:/!ba
}'
section1:
  key1: value1
  key2: value2
section2:
  key1: value1
  key2: value2
EOF
```

- `:a`表示定义一个名为`a`的跳转标签，`ba`则为跳转到`a`标签；
- `n`表示读取下一行。

删除匹配的行下一行到尾行内容：

```bash
cat <<EOF | sed '/section2:/ {
  p
  :a
  N
  $!ba
  d
}'
section1:
  key1: value1
  key2: value2
section2:
  key1: value1
  key2: value2
EOF
```

- `p`表示打印匹配行，也就是`section2:`；
- `N`表示追加下一行到模式匹配。

## 参考

<https://wangchujiang.com/linux-command/c/sed.html>
